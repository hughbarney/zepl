;;
;; ZEPL a tiny Emacs editor core with a tiny lisp extension language
;; hughbarney AT googlemail.com
;;
;; The editor provides only basic buffer movement and edit functions
;; everything else is done by extending the user interface using the
;; lisp extension language. Functions can be bound to keys using set-key.
;; For example: (set-key "c-k" "(kill-to-eol)")
;; 
;; place zepl.rc in your home direcory and it is run when zepl starts up.
;;

(defun repeat (n func)  
  (cond ((> n 0) (func) (repeat (- n 1) func))))

;; concatenate a list of strings
(defun concat(args)
  (cond
    ((null args) "")
    ((null (cdr args)) (car args))
    (t (string.append (car args) (concat (cdr args))))))

;; shrink string by dropping off last char
(defun shrink(s)
  (cond
    ((< (string.length s) 2) "")
    (t (string.substring s 0 (- (string.length s) 2)))  ))

;; prompt for string and return response, handle backspace, cr and c-g
(defun input(q response)
  (prompt q response)
  (setq key (get-key))
  (if (eq key "")
  (cond
    ((eq "backspace" (get-key-name)) (setq key "backspace"))
    (t (setq key "c-g"))))
  (cond
    ((eq key "\n") response)
    ((eq key "c-g") "")
    ((eq key "backspace") (input q (shrink response)))
    (t (input q (string.append response key)))  ))

;;
;;  Extend the Editor with some useful functions
;;

(defun duplicate_line()
  (beginning-of-line)
  (set-mark)
  (next-line)
  (beginning-of-line)
  (copy-region)
  (yank)
  (previous-line))

;; kill to end of line, uses if and progn
(defun kill-to-eol()
  (if (eq "\n" (get-char))
    (progn
      (delete))
    (progn
      (set-mark)
      (end-of-line)
      (kill-region))))

;; prompt for a keystroke then show its name
(defun describe-key()
  (prompt "Describe Key: " "")
  (setq key (get-key))
  (cond
    ((not (eq key "")) (message key))
    (t (message (concat (list (get-key-name) " runs command " (get-key-funcname)))))))

;; goto the line requested
;; (does not check for stupid responses yet)
(defun gotoline()
 (setq line (input "Goto line: " ""))
 (cond
   ((eq line "") ())
   (t (setq ln (string->number line)) (beginning-of-buffer) (repeat (- ln 1) next-line)) ))

;;
;;  Key Bindings 
;;

(set-key "esc-a" "(duplicate_line)")
(set-key "esc-g" "(gotoline)")
(set-key "c-k" "(kill-to-eol)")
(set-key "c-x ?" "(describe-key)")

;; example: this keystroke will fail as we have not defined (dobee)
;; the error will be displayed on the message line
(set-key "esc-b" "(dobee)")


