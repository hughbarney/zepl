;;
;; ZEPL a tiny Emacs editor core with a tiny lisp extension language
;; hughbarney AT googlemail.com
;;
;; The editor provides only basic buffer movement and edit functions
;; everything else is done by extending the user interface using the
;; lisp extension language. Functions can be bound to keys using set-key.
;; For example: (set-key "c-k" "(kill-to-eol)")
;; 
;; place zepl.rc in your home direcory and it is run when zepl starts up.
;;

(defun repeat (n func)  
  (cond ((> n 0) (func) (repeat (- n 1) func))))

;; concatenate a list of strings
(defun concat(args)
  (cond
    ((null args) "")
    ((null (cdr args)) (car args))
    (t (string.append (car args) (concat (cdr args))))))

;; shrink string by dropping off last char
(defun shrink(s)
  (cond
    ((< (string.length s) 2) "")
    (t (string.substring s 0 (- (string.length s) 2)))  ))

;; some keystroke checks that we will use later
(defun is_ctl_g(k)
  (eq k (ascii 7)))

(defun is_escape(k)
  (eq k (ascii 27)))

(defun is_backspace(k)
  (or (eq k (ascii 8)) (eq k (ascii 127))))

(defun is_ctl_s(k)
  (eq k (ascii 19)))

(defun is_control_char(k)
  (and (>= (ascii->number k) 0) (<= (ascii->number k) 31)))


;; prompt for string and return response, handle backspace, cr and c-g
(defun input(q response)
  (prompt q response)
  (setq key (getch))
  (cond
    ((eq key "\n") response)
    ((is_ctl_g key) "")
    ((is_backspace key) (input q (shrink response)))
    ((is_control_char key) (input q response))
    (t (input q (string.append response key)))  ))

;;
;; search function
;;
(defun search()
  (setq o_point (get-point))
  (prompt "Search: " "")
  (search_body "Search: " ""))

;; search, handle backspace, c-s, escape, cr and c-g
(defun search_body(q response)
  (setq key (getch))
  (refresh)
  (cond
    ((is_ctl_g key) (set-point o_point))
    ((is_escape key) "")
    ((is_backspace key) (prompt q (shrink response)) (search_body q (shrink response)))
    ((is_ctl_s key)
       (setq spt (search-forward (get-point) response))
       (display_search_result spt q response)
       (search_body q response))
    ((is_control_char key) (search_body q response))
    (t (prompt q (string.append response key)) (search_body q (string.append response key)))  ))

;; given a search result, update the screen or warn that it failed
(defun display_search_result(loc q search)
  (if (not (eq loc -1))
    (progn
      (set-point loc)
      (message (string.append q search))
      (display))
    (progn
      (prompt (string.append "Failing " q) search)
      (set-point 0)) ))

;;
;; duplicate a line
;;
(defun duplicate_line()
  (beginning-of-line)
  (set-mark)
  (next-line)
  (beginning-of-line)
  (copy-region)
  (yank)
  (previous-line))

;; kill to end of line, uses if and progn
(defun kill-to-eol()
  (if (eq "\n" (get-char))
    (progn
      (delete))
    (progn
      (set-mark)
      (end-of-line)
      (kill-region))))

;; prompt for a keystroke then show its name
(defun describe-key()
  (prompt "Describe Key: " "")
  (setq key (get-key))
  (cond
    ((not (eq key "")) (message key))
    (t (message (concat (list (get-key-name) " runs command " (get-key-funcname)))))))

;; goto the line requested
;; (does not check for stupid responses yet)
(defun gotoline()
 (setq line (input "Goto line: " ""))
 (cond
   ((eq line "") ())
   (t (setq ln (string->number line)) (beginning-of-buffer) (repeat (- ln 1) next-line)) ))

;;
;;  Key Bindings 
;;

(set-key "esc-a" "(duplicate_line)")
(set-key "esc-g" "(gotoline)")
(set-key "c-k" "(kill-to-eol)")
(set-key "c-s" "(search)")
(set-key "c-x ?" "(describe-key)")

